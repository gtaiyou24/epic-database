terraform {
  required_providers {
    google = {
      source  = "hashicorp/google"
      version = "5.34.0"
    }
  }
}
# ========== ğŸ—“ï¸ Cloud Run Job ã‚’å®šç¾©ã™ã‚‹ ==========

# ğŸ¤– Subscriber ã‚µãƒ¼ãƒ“ã‚¹ã®ã‚µãƒ¼ãƒ“ã‚¹ã‚¢ã‚«ã‚¦ãƒ³ãƒˆã‚’ä½œæˆã™ã‚‹
resource "google_service_account" "batch" {
  account_id   = "batcher"
  display_name = "Batch ã‚µãƒ¼ãƒ“ã‚¹ã‚¢ã‚«ã‚¦ãƒ³ãƒˆ"
  description  = "Cloud Run Job / Batch ã®ã‚µãƒ¼ãƒ“ã‚¹ã‚¢ã‚«ã‚¦ãƒ³ãƒˆ"
}
resource "google_project_iam_member" "iam_service_account_user" {
  project = var.project_id
  role    = "roles/iam.serviceAccountUser"  # ã‚µãƒ¼ãƒ“ã‚¹ã‚¢ã‚«ã‚¦ãƒ³ãƒˆãƒ¦ãƒ¼ã‚¶ãƒ¼
  member  = "serviceAccount:${google_service_account.batch.email}"
}
resource "google_project_iam_member" "run_admin" {  # Cloud Run ã®ç®¡ç†è€…æ¨©é™
  project = var.project_id
  role    = "roles/run.admin"  # Cloud Run ã®ç®¡ç†è€…æ¨©é™
  member  = "serviceAccount:${google_service_account.batch.email}"
}

# ğŸ³ Artifact Registry ã‚’ä½œæˆ
resource "google_artifact_registry_repository" "subscriber" {
  provider      = google
  location      = var.region
  repository_id = "batch"
  format        = "DOCKER"
}

# âš¡ï¸ Cloud Run Job ã‚’ä½œæˆ
resource "google_cloud_run_v2_job" "batch" {
  provider = google-beta
  project  = var.project_id
  location = var.region
  name     = "batch"

  template {
    template {
      timeout         = "43200s"  # 12æ™‚é–“
      max_retries     = 0   # ãƒªãƒˆãƒ©ã‚¤å›æ•°
      service_account = google_service_account.batch.email
      containers {
        image   = var.image
        args    = ["api-gateway", "HealthCheck.1"]
      }
    }
  }
}

# ========== ğŸ—“ï¸ Scheduler ã‚’å®šç¾©ã™ã‚‹ ==========

locals {
  jobs = {
    "health_check" = {
      description = "Batch ãƒ˜ãƒ«ã‚¹ãƒã‚§ãƒƒã‚¯"
      schedule    = "0 * * * *"
      args        = ["api-gateway", "HealthCheck.1"]
    },
    "download_gbizinfo" = {
      description = "gBizINFO ã‹ã‚‰æ³•äººãƒ‡ãƒ¼ã‚¿ã‚’ãƒ€ã‚¦ãƒ³ãƒ­ãƒ¼ãƒ‰ã™ã‚‹"
      schedule    = "23 01 1 * *"    # æ¯æœˆã®*æœˆ1æ—¥ã®1æ™‚23åˆ†ã«ã‚¯ãƒ­ãƒ¼ãƒ³èµ·å‹•ã‚’è¨­å®šã™ã‚‹ã€‚
      args        = ["crawler", "DownloadgBizINFO.1"]
    }
    // ã‚¸ãƒ§ãƒ–ã‚’è¿½è¨˜ã™ã‚‹
  }
}
resource "google_cloud_scheduler_job" "cron" {
  for_each    = local.jobs
  name        = "batch-${each.key}"
  description = each.value.description
  schedule    = each.value.schedule
  time_zone   = "Asia/Tokyo"

  retry_config {
    max_backoff_duration = "3600s"
    max_doublings        = 5
    max_retry_duration   = "0s"
    min_backoff_duration = "300s"
    retry_count          = 0
  }

  http_target {
    http_method = "POST"
    uri         = "https://${google_cloud_run_v2_job.batch.location}-run.googleapis.com/apis/run.googleapis.com/v1/namespaces/${var.project_number}/jobs/${google_cloud_run_v2_job.batch.name}:run"
    oauth_token {
      service_account_email = google_service_account.batch.email
    }
    body = base64encode(jsonencode({
      overrides = {
        containerOverrides = [{
          args = each.value.args
        }]
      }
    }))
  }

  depends_on = [google_cloud_run_v2_job.batch, google_service_account.batch]
}