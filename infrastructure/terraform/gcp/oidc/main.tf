terraform {
  required_providers {
    google = {
      source  = "hashicorp/google"
      version = "6.10.0"
    }
  }
}
# GCPãƒ—ãƒ­ãƒã‚¤ãƒ€ãƒ¼ ã®è¨­å®š
provider "google" {
  project = var.project_id
  region  = var.region
}

# ğŸ¤– GitHub Actions ã®ã‚µãƒ¼ãƒ“ã‚¹ã‚¢ã‚«ã‚¦ãƒ³ãƒˆã‚’ä½œæˆã™ã‚‹
resource "google_service_account" "github_actions" {
  account_id   = "github-actions"
  display_name = "GitHub Actions ã‚µãƒ¼ãƒ“ã‚¹ã‚¢ã‚«ã‚¦ãƒ³ãƒˆ"
  description  = "GitHub Actions ãŒ GCP ã¸ã‚¢ãƒ—ãƒªã‚’ãƒ‡ãƒ—ãƒ­ã‚¤ã™ã‚‹ãŸã‚ã®ã‚µãƒ¼ãƒ“ã‚¹ã‚¢ã‚«ã‚¦ãƒ³ãƒˆ"
}
# ğŸ” GitHub Actions ã‚µãƒ¼ãƒ“ã‚¹ã‚¢ã‚«ã‚¦ãƒ³ãƒˆã«ãƒ­ãƒ¼ãƒ«ã‚’ä»˜ä¸
resource "google_project_iam_member" "run-admin" {
  project = var.project_id
  role    = "roles/run.admin"  # Cloud Run ãƒ‡ãƒ—ãƒ­ã‚¤æ¨©é™ã‚’ä»˜ä¸
  member  = "serviceAccount:${google_service_account.github_actions.email}"
}
resource "google_project_iam_member" "artifact-registry-repo-admin" {
  project = var.project_id
  role    = "roles/artifactregistry.repoAdmin"  # Artifact Registry ã¸ã®ãƒ—ãƒƒã‚·ãƒ¥ã€å‰Šé™¤ã‚’ã™ã‚‹ãŸã‚ã®ãƒ­ãƒ¼ãƒ«
  member  = "serviceAccount:${google_service_account.github_actions.email}"
}
resource "google_service_account_iam_member" "iam-service-account-user" {
  for_each           = { for sa in var.deployment_service_account_ids : sa => "projects/${var.project_id}/serviceAccounts/${sa}" }
  service_account_id = each.value
  role               = "roles/iam.serviceAccountUser"  # GitHub Actions ã‚µãƒ¼ãƒ“ã‚¹ã‚¢ã‚«ã‚¦ãƒ³ãƒˆã« Cloud Run ãªã©ã®ã‚³ãƒ³ãƒ”ãƒ¥ãƒ¼ãƒ†ã‚£ãƒ³ã‚°ã‚’ãƒ‡ãƒ—ãƒ­ã‚¤ã™ã‚‹æ¨©é™ã‚’ä»˜ä¸
  member             = "serviceAccount:${google_service_account.github_actions.email}"
}

# ğŸ› ï¸ Workload Identity ãƒ—ãƒ¼ãƒ«ãƒ»ãƒ—ãƒ­ãƒã‚¤ãƒ€ã‚’ä½œæˆã™ã‚‹
resource "google_iam_workload_identity_pool" "github_actions_oidc" {  # Workload Identity ãƒ—ãƒ¼ãƒ«ã‚’ä½œæˆ
  workload_identity_pool_id = "github-actions-oidc"
  display_name              = "GitHub Actions OIDC"
  project                   = var.project_id
}
resource "google_iam_workload_identity_pool_provider" "github_actions_provider" {  # Workload Identity ãƒ—ãƒ­ãƒã‚¤ãƒ€ã‚’ä½œæˆ
  workload_identity_pool_id = google_iam_workload_identity_pool.github_actions_oidc.workload_identity_pool_id
  workload_identity_pool_provider_id = "github-actions-oidc-provider"
  display_name = "GitHub Actions OIDC Provider"
  attribute_condition = "assertion.repository == \"${var.github_repo_owner}/${var.github_repo_name}\""

  attribute_mapping = {
    "google.subject"          = "assertion.sub"
    "attribute.repository"    = "assertion.repository"
    "attribute.actor"         = "assertion.actor"
  }
  oidc {
    issuer_uri = "https://token.actions.githubusercontent.com"
  }
}

# ğŸ‘Œ Workload Identity ãƒ—ãƒ­ãƒã‚¤ãƒ€ãŒè©²å½“ã®ã‚µãƒ¼ãƒ“ã‚¹ã‚¢ã‚«ã‚¦ãƒ³ãƒˆã®æ¨©é™ã‚’å€Ÿç”¨ã™ã‚‹ã“ã¨ã‚’è¨±å¯ã™ã‚‹
resource "google_service_account_iam_binding" "allow_workload_identity_user" {
  service_account_id = google_service_account.github_actions.name
  role = "roles/iam.workloadIdentityUser"
  members = [
    "principalSet://iam.googleapis.com/projects/${var.project_number}/locations/global/workloadIdentityPools/${google_iam_workload_identity_pool.github_actions_oidc.workload_identity_pool_id}/attribute.repository/${var.github_repo_owner}/${var.github_repo_name}"
  ]
}